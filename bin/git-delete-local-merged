#!/usr/bin/env fish
#
# Deletes local branches that have been merged into the main branch.
# Also detects and removes branches that were squash-merged.
#
# Examples
#
#   git delete-local-merged

if contains -- --help $argv; or contains -- -h $argv
    echo "Usage: git delete-local-merged"
    echo ""
    echo "Deletes local branches that have been merged into the main branch."
    echo "Also detects and removes branches that were squash-merged (GitHub's"
    echo "'Squash and merge' button)."
    echo ""
    echo "Examples:"
    echo "  git delete-local-merged   # Clean up all merged branches"
    exit 0
end

function log
	echo (set_color --bold magenta) "---> $argv" (set_color normal)
end

set main_branch (git main-branch)

log "Removing merged branches..."
set branches (git branch --merged | grep -v "^\*" | grep -v $main_branch | tr -d '\n')
if test -n $branches
	echo "$branches" | xargs git branch -d
end

log "Removing branches squashed or merged into $main_branch..."
git for-each-ref refs/heads/ --format="%(refname:short)" | while read branch
	set base (git merge-base $main_branch $branch)
	set hash (git rev-parse "$branch^{tree}")
	set commit (git commit-tree $hash -p $base -m _)
	if test (git cherry $main_branch $commit) = "- $commit"
		git branch -D $branch
	end
end
